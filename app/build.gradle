plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.4.30'
}

android {
    compileSdkVersion 31

    defaultConfig {
        applicationId "ru.rtuitlab.itlab"
        minSdkVersion 24
        targetSdkVersion 31
        versionCode 1
        versionName "1.0"

        manifestPlaceholders = [
                'appAuthRedirectScheme': 'ru.rtuitlab.itlab'
        ]

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        def API_URI = "API_URI"
        def ISSUER_URI = "ISSUER_URI"
        def CLIENT_ID = "CLIENT_ID"
        def REDIRECT_URI = "REDIRECT_URI"
        def API_SCOPES = "SCOPES"

        def CONFIG = { k -> "\"${project.properties.get(k)}\"" }
        def CONFIG_RAW = { k -> "${project.properties.get(k)}" }

        debug {
            buildConfigField ("String", API_URI, CONFIG("debug.api.uri"))
            buildConfigField ("String", ISSUER_URI, CONFIG("debug.issuer.uri"))
            buildConfigField ("String", CLIENT_ID, CONFIG("client.id"))
            buildConfigField ("String", REDIRECT_URI, CONFIG("redirect.uri"))
            buildConfigField ("String[]", API_SCOPES, CONFIG_RAW("api.scopes"))
        }
        release {
            buildConfigField ("String", API_URI, CONFIG("release.api.uri"))
            buildConfigField ("String", ISSUER_URI, CONFIG("release.issuer.uri"))
            buildConfigField ("String", CLIENT_ID, CONFIG("client.id"))
            buildConfigField ("String", REDIRECT_URI, CONFIG("redirect.uri"))
            buildConfigField ("String[]", API_SCOPES, CONFIG_RAW("api.scopes"))

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        useIR = true
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }

    signingConfigs {
        release {
            storeFile file("../keystore/keystore.jks")
            storePassword System.getenv("SIGNING_STORE_PASSWORD")
            keyAlias System.getenv("SIGNING_KEY_ALIAS")
            keyPassword System.getenv("SIGNING_KEY_PASSWORD")
        }
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.activity:activity-compose:1.3.0-alpha02'

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.1'

    // Test
    testImplementation 'junit:junit:4.13.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    // Compose
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.ui:ui-tooling:$compose_version"
    implementation "androidx.compose.material:material-icons-extended:$compose_version"
    implementation "androidx.compose.runtime:runtime-livedata:$compose_version"
    implementation "androidx.navigation:navigation-compose:2.4.0-alpha10"
    implementation "com.google.accompanist:accompanist-flowlayout:0.19.0"

    // Hilt
    implementation "com.google.dagger:hilt-android:$hilt_version"
    implementation "androidx.hilt:hilt-navigation:1.0.0"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"

    // Preferences DataStore
    implementation "androidx.datastore:datastore-preferences:1.0.0-alpha06"

    // OpenID auth
    implementation 'net.openid:appauth:0.7.1'

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:2.6.4"

    // Serialization
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.0.1"
    implementation "com.jakewharton.retrofit:retrofit2-kotlinx-serialization-converter:0.8.0"

    // Time
    implementation 'org.jetbrains.kotlinx:kotlinx-datetime:0.3.0'
}